apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    namespace 'com.jdamcd.sudoku'
    compileSdk 35

    defaultConfig {
        applicationId 'com.jdamcd.sudoku'
        minSdkVersion 23
        targetSdkVersion 35
        flavorDimensions 'default'
        versionCode Integer.parseInt(project.SUDOKU_VERSION_CODE)
        versionName project.SUDOKU_VERSION_NAME + '-' + versionCode
        project.ext.set('archivesBaseName', 'sudoku-' + versionName)

        testApplicationId "com.jdamcd.sudoku.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    productFlavors {
        google {
            buildConfigField 'boolean', 'GOOGLE', 'true'
        }

        vanilla {
            buildConfigField 'boolean', 'GOOGLE', 'false'
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    lint {
        disable 'IncludeLayoutParam'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    ext.room_version = '2.6.1'

    implementation project(':core')

    // Support
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.6'

    // Google
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.play:review:2.0.2'

    // Core
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Architecture
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Unit tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.15.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:5.4.0'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'androidx.annotation:annotation:1.9.1'

    // Android tests
    androidTestImplementation 'org.assertj:assertj-core:3.27.3'
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation "androidx.test.ext:junit:1.2.1"

    androidTestUtil 'androidx.test:orchestrator:1.5.1'
}
